
find_package(Doxygen REQUIRED)

set(FA_DOXYGEN_INPUT_DIR ${PROJECT_SOURCE_DIR}/include)
set(FA_DOXYGEN_OUTPUT_DIR ${CMAKE_CURRENT_BINARY_DIR}/doxygen)
set(FA_DOXYGEN_INDEX_FILE ${FA_DOXYGEN_OUTPUT_DIR}/xml/index.xml)

set(FA_DOXYFILE_IN ${CMAKE_CURRENT_SOURCE_DIR}/Doxyfile.in)
set(FA_DOXYFILE_OUT ${CMAKE_CURRENT_BINARY_DIR}/Doxyfile)
# Set the Doxygen input and output directories in the Doxyfile
configure_file(${FA_DOXYFILE_IN} ${FA_DOXYFILE_OUT} @ONLY)
# Doxygen won't create this for us
file(MAKE_DIRECTORY ${FA_DOXYGEN_OUTPUT_DIR})

file(GLOB_RECURSE FA_PUBLIC_HEADERS ${FA_DOXYGEN_INPUT_DIR}/*.h)

add_custom_command(OUTPUT ${FA_DOXYGEN_INDEX_FILE}
	DEPENDS ${FA_PUBLIC_HEADERS}
	COMMAND ${DOXYGEN_EXECUTABLE} ${FA_DOXYFILE_OUT}
	MAIN_DEPENDENCY ${FA_DOXYFILE_OUT} ${FA_DOXYFILE_IN}
	COMMENT "Generating docs"
	VERBATIM)
add_custom_target(FA_Doxygen ALL DEPENDS ${FA_DOXYGEN_INDEX_FILE})
set(FA_SPHINX_SOURCE ${CMAKE_CURRENT_SOURCE_DIR}/source)
set(FA_SPHINX_BUILD ${CMAKE_CURRENT_BINARY_DIR}/sphinx)
add_custom_target(FA_Sphinx ALL
		  COMMAND
		  sphinx-build -b html
		  -Dbreathe_projects.${PROJECT_NAME}=${FA_DOXYGEN_OUTPUT_DIR}/xml
		  ${FA_SPHINX_SOURCE} ${FA_SPHINX_BUILD}
		  WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
		  COMMENT "Generating documentation with Sphinx")
